### PREPROCESSOR and FLAGS
QMAKE_LFLAGS += -Wl,--enable-auto-import
QMAKE_CXXFLAGS_WARN_OFF += -Wunused-parameter

DEFINES += __x86__ \
    __NT__ \
    __OSVERSION__=4 \
    __WIN32__ \
    WIN32 \
    _WINDOWS \
    _USRDLL \
    _CRT_SECURE_NO_DEPRECATE \
    _WIN32_WINNT=0x0400 \
    _MBCS

### FOLDERS, INCLUDES

CONFIG(debug, debug|release){
    UI_DIR = debug/generatedfiles/ui
    MOC_DIR = debug/generatedfiles/moc
    RCC_DIR = debug/generatedfiles/rcc
    OBJECTS_DIR = debug/generatedfiles

    INCLUDEPATH  += ../../OMOptimBasis/build/debug/generatedfiles/ui
    INCLUDEPATH  += debug/generatedfiles/ui

}else {
    UI_DIR = release/generatedfiles/ui
    MOC_DIR = release/generatedfiles/moc
    RCC_DIR = release/generatedfiles/rcc
    OBJECTS_DIR = release/generatedfiles

    INCLUDEPATH  += ../../OMOptimBasis/build/release/generatedfiles/ui
    INCLUDEPATH  += release/generatedfiles/ui
}

  # win32 vs. win64
  UNAME = $$system(uname)
  isEmpty(UNAME): UNAME = MINGW32
  ISMINGW32 = $$find(UNAME, MINGW32)
  message(uname: $$UNAME)
  count( ISMINGW32, 1 ) {
    CORBAINC = $$(OMDEV)/lib/omniORB-4.2.0-mingw32/include
    CORBALIBS = -L$$(OMDEV)/lib/omniORB-4.2.0-mingw32/lib/x86_win32 -lomniORB420_rt -lomnithread40_rt -lomniDynamic420_rt
    DEFINES += __x86__ \
               __NT__ \
               __OSVERSION__=4 \
               __WIN32__
  } else {
    CORBAINC = $$(OMDEV)/lib/omniORB-4.2.0-mingw64/include
    CORBALIBS = -L$$(OMDEV)/lib/omniORB-4.2.0-mingw64/lib/x86_win32 -lomniORB420_rt -lomnithread40_rt -lomniDynamic420_rt
    DEFINES += __x86__ \
	           __x86_64__ \
	           __NT__ \
               __OSVERSION__=4 \
			   __WIN32__ \
			   _WIN64 \
			   MS_WIN64
  }


INCLUDEPATH += $${CORBAINC} \
               $$(OMBUILDDIR)/include/omplot/qwt \
               $$(OMBUILDDIR)/include/omc/c/util \
               ../../ParadisEO-2.0.1/eo/src \
               ../../ParadisEO-2.0.1/eo/src/utils \
               ../../ParadisEO-2.0.1/moeo/src \
               ../../ParadisEO-2.0.1/mo/src

### LIBRARIES (order has an impact !!)
CONFIG(debug, debug|release){
    LIBS += $${CORBALIBS} -L$$(OMBUILDDIR)/lib/omc -lOMOptimBasisd -lomqwtd
}else {
    LIBS += $${CORBALIBS} -L$$(OMBUILDDIR)/lib/omc -lOMOptimBasis -lomqwt
}

LIBS += -L../../ParadisEO-2.0.1/build/eo/lib -leo -leoutils \
    -L../../ParadisEO-2.0.1/build/moeo/lib -lmoeo
